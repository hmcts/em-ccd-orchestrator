buildscript {
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'org.owasp.dependencycheck' version '3.1.2'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'org.sonarqube' version '2.6.2'
    id "net.ltgt.apt-eclipse" version "0.18"
    id "net.ltgt.apt-idea" version "0.18"
    id "net.ltgt.apt" version "0.18"
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
   // id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.0.11"
    id "com.github.lkishalmi.gatling" version "3.0.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'idea'

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-openfeign:2.0.2.BUILD-SNAPSHOT'
    }
}

if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}

group 'uk.gov.hmcts.reform.em.orchestrator'
version '0.0.1'
sourceCompatibility = 1.8
mainClassName = 'uk.gov.hmcts.reform.em.orchestrator.Application'

configurations {
    aatCompile.extendsFrom(testCompile)
    aatRuntime.extendsFrom(testRuntime)
}

checkstyle {
    maxWarnings = 10
    toolVersion = '8.12'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.11.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)

repositories {
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    jcenter()
    mavenLocal()
    maven {
        url 'https://repo.spring.io/libs-snapshot'
    }
}

dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

def versions = [
        springBoot         : '2.1.2.RELEASE',
        springfoxSwagger   : '2.7.0',
        jackson            : '2.9.8'
]

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile("org.springframework.boot:spring-boot-starter-batch")
    compile "com.fasterxml.jackson.datatype:jackson-datatype-json-org:${versions.jackson}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hppc:${versions.jackson}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:${versions.jackson}"
    compile "org.zalando:problem-spring-web:0.24.0-RC.0"
    compile("io.springfox:springfox-swagger2:${versions.springfoxSwagger}")
    compile("io.springfox:springfox-swagger-ui:${versions.springfoxSwagger}")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile( 'org.apache.httpcomponents:httpclient:4.5.5')
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '2.1.1'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
    compile "org.ehcache:ehcache"
    compile group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.1'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.1'

    compile 'org.springframework.cloud:spring-cloud-starter-openfeign'

    compile "org.mapstruct:mapstruct-jdk8:${mapstruct_version}"
    compile "org.springframework.boot:spring-boot-starter-security"

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"
    annotationProcessor ("org.springframework.boot:spring-boot-configuration-processor") {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.github.gmazzo:okhttp-mock:1.0.4'
    testCompile 'io.rest-assured:rest-assured:3.2.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: '5.0.1'
    compile "pl.touk:throwing-function:1.3"
    compile "com.jayway.jsonpath:json-path:2.4.0"
}

bootJar {
    baseName 'rpa-em-ccd-orchestrator'
    archiveName = "$baseName"+".jar"
    destinationDir = file("$rootDir/build/libs")
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

def coverageExclusionList = [
    '**uk/gov/hmcts/reform/em/orchestrator/Application*',
    '**uk/gov/hmcts/reform/em/orchestrator/appinsights/*',
    '**uk/gov/hmcts/reform/em/orchestrator/config/*',
    '**uk/gov/hmcts/reform/em/orchestrator/config/logging/*',
    '**uk/gov/hmcts/reform/em/orchestrator/domain/*',
    '**uk/gov/hmcts/reform/em/orchestrator/service/dto/external/annotation/*',
]

//(jacocoTestReport | jacocoTestCoverageVerification)
jacoco {
    toolVersion = '0.7.9' // jacocoMavenPluginVersion
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        csv.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusionList)
        })
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.exclusions", coverageExclusionList.join(", ")
    }
}

sourceSets {
    aat {
        scala {
            srcDirs = ['src/aat/java']
        }
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir('src/aat/java')
        }
        resources {
            srcDir('src/aat/resources')
        }
        compileClasspath += configurations.gatling
    }
    test {
        java.srcDir 'src/test/java'
    }
}

configurations {
    gatling
}

gatling {
    logLevel = "INFO"
}

task gatlingRunAAT(type: JavaExec) {
    group = 'gatling'
    description = 'Run gatling tests'
    new File("${buildDir}/reports/tests/performance").mkdirs()
    classpath = sourceSets.aat.runtimeClasspath + configurations.gatling
    main = "io.gatling.app.Gatling"
    args = ['-s', 'uk.gov.hmcts.reform.em.orchestrator.performance.NpaLoad',
            '-rf', "${buildDir}/reports/tests/performance"
    ]
}

task gatlingRunSmoke(type: JavaExec) {
    group = 'gatling'
    description = 'Run gatling tests'
    new File("${buildDir}/reports/tests/performance").mkdirs()
    classpath = sourceSets.aat.runtimeClasspath + configurations.gatling
    main = "io.gatling.app.Gatling"
    args = ['-s', 'uk.gov.hmcts.reform.em.orchestrator.performance.NpaSmoke',
            '-rf', "${buildDir}/reports/tests/performance"
    ]
}

task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/orchestrator/functional/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task smoke(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/orchestrator/smoke/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

dependencyCheck {
    suppressionFile = 'dependency-check-suppressions.xml'
}

compileJava.dependsOn processResources
