buildscript {
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.6.0")
        classpath("net.serenity-bdd:serenity-single-page-report:2.6.0")
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.springframework.boot' version '3.1.5'
    id 'uk.gov.hmcts.java' version '0.12.48'
    id 'com.github.ben-manes.versions' version '0.50.0'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'jacoco'
    id 'checkstyle'
    id 'au.com.dius.pact' version '4.2.13'
    id "info.solidsoft.pitest" version '1.9.0'
    id 'org.owasp.dependencycheck' version '8.4.3'
}

apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'


if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}

group 'uk.gov.hmcts.reform.em.orchestrator'
version '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

mainClassName = 'uk.gov.hmcts.reform.em.orchestrator.Application'

configurations {
    aatImplementation.extendsFrom(testImplementation)
    aatRuntimeOnly.extendsFrom(testRuntimeOnly)
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

def versions = [
        serenity           : '3.7.0',
        serenityreporter   : '3.7.0',
        serenityRestAssured: '3.7.0',
        pact_version       : '4.0.10',
        mockito            : '5.7.0',
        jupiter            : '5.8.2',
        poi                : '5.2.4',
        springSecurity     : '6.1.5',
        springCloud        : '4.0.4',
        okhttp3            : '4.12.0',
]
sourceSets {
    aat {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java.srcDir 'src/test/java'
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations.all {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
    exclude group: 'org.glassfish', module: 'jakarta.el'
    exclude group: 'org.springframework.retry', module: 'spring-retry'
    exclude group: 'org.springframework.security', module: 'spring-security-rsa'
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
    exclude group: 'com.ibm.icu', module: 'icu4j'
    exclude group: 'org.codehaus.jettison', module: 'jettison'
}

dependencies {

    def withoutStuff = {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }

    def withoutPactExtras = {
        exclude group: 'org.jetbrains.kotlinx',   module: 'kotlinx-coroutines-io'
        exclude group: 'org.jetbrains.kotlinx',   module: 'kotlinx-io'
        exclude group: 'org.eclipse.jetty.alpn', module: 'alpn-api'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-batch'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

    implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: versions.okhttp3
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: versions.okhttp3
    implementation group: "commons-io", name: "commons-io", version: "2.15.0"

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: versions.springCloud
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloud

    implementation group: 'pl.touk', name : 'throwing-function', version: '1.3'
    implementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.8.0'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.2.0'
    implementation group: "org.apache.commons", name: "commons-lang3", version: "3.14.0"
    implementation group: "commons-validator", name: "commons-validator", version: "1.7"

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.1.1'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.0.1'
    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.59'
    implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '4.9.2'

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.16.0'

    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springSecurity

    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '13.1'
    implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '4.1.0-RELEASE'

    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', withoutStuff
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    aatAnnotationProcessor group:'org.projectlombok', name: 'lombok', version: '1.18.30'

    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0', classifier: 'all', {
        exclude group: 'commons-io', module: 'commons-io'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', withoutStuff
    testImplementation group: 'com.github.gmazzo', name:'okhttp-mock', version:'2.0.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: versions.mockito
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: versions.mockito

    testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenityRestAssured, withoutStuff
    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenityreporter

    testRuntimeOnly group: 'org.junit.vintage', name :'junit-vintage-engine', version: versions.jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.jupiter
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter

    aatImplementation group: 'org.apache.poi', name: 'poi', version: versions.poi
    aatImplementation group: 'org.apache.poi', name: 'poi-ooxml', version: versions.poi
    aatRuntimeOnly group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.1'
    aatRuntimeOnly group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '4.0.4'
    
    //pact contract testing
    contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-junit', version: versions.pact_version, withoutPactExtras
    contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-junit5', version: versions.pact_version, withoutPactExtras
    contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-java8', version: versions.pact_version, withoutPactExtras
    implementation group: 'au.com.dius', name: 'pact-jvm-consumer', version: versions.pact_version, withoutPactExtras

    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.jupiter
    contractTestRuntimeOnly  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    aatImplementation 'com.github.hmcts:em-test-helper:2.0.4'
}

dependencyManagement {
    dependencies {
        //CVE-2022-23437
        dependencySet(group: 'xerces', version: '2.12.2') {
            entry 'xercesImpl'
        }

        //CVE-2022-1471
        dependencySet(group: 'org.yaml', version: '2.2') {
            entry 'snakeyaml'
        }

        //CVE-2023-24998
        dependencySet(group: 'commons-fileupload', version: '1.5') {
            entry 'commons-fileupload'
        }

        //CVE-2020-13956
        dependencySet(group: 'org.apache.httpcomponents', version: '4.5.14') {
            entry 'fluent-hc'
        }

        //CVE-2023-3635
        dependencySet(group: 'com.squareup.okio', version: '3.6.0') {
            entry 'okio-jvm'
        }

        //CVE-2023-41080
        dependencySet(group: 'org.apache.tomcat.embed', version: '10.1.16') {
            entry 'tomcat-embed-websocket'
            entry 'tomcat-embed-core'
        }

        // CVE-2023-44487
        dependencySet(group: 'io.netty', version: '4.1.101.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-dns'
            entry 'netty-codec-http'
            entry 'netty-codec-http2'
            entry 'netty-codec-socks'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-handler-proxy'
            entry 'netty-resolver'
            entry 'netty-resolver-dns'
            entry 'netty-resolver-dns-classes-macos'
            entry 'netty-resolver-dns-native-macos'
            entry 'netty-transport'
            entry 'netty-transport-classes-epoll'
            entry 'netty-transport-classes-kqueue'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
        }     
    }
}

checkstyle {
//    ignoreFailures = true
    maxWarnings = 10
    toolVersion = '10.12.5'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}


checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencyCheck {
    suppressionFile = 'config/owasp/dependency-check-suppressions.xml'
}

bootJar {
    getArchiveFileName().set(provider {
        'rpa-em-ccd-orchestrator.jar'
    })
    destinationDirectory = file("$rootDir/build/libs")
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

def coverageExclusionList = [
        '**uk/gov/hmcts/reform/em/orchestrator/Application*',
        '**uk/gov/hmcts/reform/em/orchestrator/CliApplication*',
        '**uk/gov/hmcts/reform/em/orchestrator/config/**/*',
        '**uk/gov/hmcts/reform/em/orchestrator/domain/*',
        '**uk/gov/hmcts/reform/em/orchestrator/stitching/dto/*',
        '**uk/gov/hmcts/reform/em/orchestrator/info/*',
        '**uk/gov/hmcts/reform/em/orchestrator/service/dto/*',
        '**uk/gov/hmcts/reform/em/orchestrator/service/ccdcallbackhandler/CdamDetailsDto*',
        '**uk/gov/hmcts/reform/em/orchestrator/service/ccdcallbackhandler/CcdCallbackResponseDto*',
]

jacocoTestReport {
    executionData(test)

    reports {
        xml.required = true
        csv.required = false

        xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.em.orchestrator.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.em.orchestrator.domain.*' ,
            'uk.gov.hmcts.reform.em.orchestrator.config.*',
            'uk.gov.hmcts.reform.em.orchestrator.info.*',
            'uk.gov.hmcts.reform.em.orchestrator.Application.java',
            'uk.gov.hmcts.reform.em.orchestrator.CliApplication.java'
    ]

    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation ='build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml','json')
    outputFormats = ['XML', 'HTML','CSV']
    mutationThreshold = 5
    coverageThreshold = 0
    jvmArgs = ['-Xms1G','-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true
}

project.tasks['sonarqube'].group = "Verification"

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
        property "sonar.exclusions", coverageExclusionList.join(", ")
        property "sonar.test.exclusions", "**/test/**"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"

        property "sonar.issue.ignore.multicriteria", "e1"
        property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S2629"
        property "sonar.issue.ignore.multicriteria.e1.resourceKey", "src/main/java/uk/gov/hmcts/reform/em/orchestrator/**"
    }
}

configurations {
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}


idea {
    module {
        testSourceDirs += project.sourceSets.aat.java.srcDirs
        testResourceDirs += project.sourceSets.aat.resources.srcDirs
        testSourceDirs += project.sourceSets.contractTest.java.srcDirs
        testResourceDirs += project.sourceSets.contractTest.resources.srcDirs
    }
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

test {
    useJUnitPlatform()
}

task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/orchestrator/functional/**"

    testLogging {
        exceptionFormat = 'full'
    }
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

// Serenity Single page Reports
// functional.finalizedBy(aggregate)

task smoke(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/orchestrator/smoke/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task validateYaml(type: JavaExec){
    description = "Validate the automated bundling YAML configuration"
    mainClass.set("uk.gov.hmcts.reform.em.orchestrator.CliApplication")
    classpath = sourceSets.main.runtimeClasspath
    args '--validate'
}

compileJava.dependsOn processResources

task contract(type: Test) {
    group = 'Delivery pipeline'
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include "uk/gov/hmcts/reform/em/orchestrator/**"
    systemProperty 'pact.rootDir', "pacts"
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task runAndPublishConsumerPactTests(type: Test){
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

task fortifyScan(type: JavaExec)  {
    mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

serenity {
    reports = ["single-page-html"]
}

pact {
    publish {
        pactDirectory = 'pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
        version = project.pactVersion
    }
}

task loadEnvSecrets() {
    doLast {
        if (!project.file('./.aat-env').exists()) {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'em-ccdorc-aat', '-o', 'tsv', '--query', 'value', '--name', 'em-ccdorc-dot-env'
                    standardOutput = os
                }
                project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8))
            }
        }
    }
}

bootRun {
    dependsOn(loadEnvSecrets)

    doFirst() {
        if (project.file('./.aat-env').exists()) {
            project.file('./.aat-env').readLines().each() {
                def index = it.indexOf("=")
                def key = it.substring(0, index)
                def value = it.substring(index + 1)
                environment key, value
            }
        }
    }
}

functional {
    doFirst() {
        if (project.file('./.aat-env').exists()) {
            project.file('./.aat-env').readLines().each() {
                def index = it.indexOf("=")
                def key = it.substring(0, index)
                def value = it.substring(index + 1)
                environment key, value
            }
        }
    }
}
